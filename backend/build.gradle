plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'com.google.cloud.tools.jib' version '3.4.3'
}

group = 'com.max'
version = '0.1.0'

java {
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
}

repositories { mavenCentral() }

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.hibernate.orm:hibernate-community-dialects:6.5.2.Final'
    implementation 'mysql:mysql-connector-java:8.0.33'

    runtimeOnly 'mysql:mysql-connector-java'
    implementation 'org.flywaydb:flyway-core:10.16.0'
    implementation 'org.flywaydb:flyway-mysql:10.16.0'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.withType(Test).configureEach { useJUnitPlatform() }

tasks.register('copyFrontend', Copy) {
    dependsOn(':frontend:buildFrontend')
    from("${project(':frontend').layout.buildDirectory.get()}/dist")
    into("$projectDir/src/main/resources/static")
}

tasks.processResources {
    dependsOn tasks.named('copyFrontend')
}

jib {
    from {
        image = 'eclipse-temurin:21-jre'
    }
    to {
        image = "docker.io/madmaximillion/simplerewards:${project.version}"
        tags = ['latest']
    }
    container {
        mainClass = 'com.madmaximillion.simplerewards.SimpleRewardsApplication'
        ports = ['8080']
        environment = [
                'JAVA_TOOL_OPTIONS': '-XX:+UseZGC -XX:MaxRAMPercentage=75',
                'APP_JWT_SECRET': 'change-me'
        ]
        user = '1000:1000'
    }
}